buildscript {
    repositories {
        maven {
            name = 'gradle plugins'
            url "https://plugins.gradle.org/m2/"
        }
        maven { url = 'https://maven.minecraftforge.net' }
        maven { url = 'https://repo.spongepowered.org/repository/maven-public/' }
        maven {
            url "https://cursemaven.com"
            content {
                includeGroup "curse.maven"
            }
        }
        maven { url = 'https://maven.parchmentmc.org' }
        mavenCentral()
    }
    dependencies {
        classpath group: 'net.minecraftforge.gradle', name: 'ForgeGradle', version: '5.1.+', changing: true
        classpath group: 'org.spongepowered', name: 'mixingradle', version: '0.7-SNAPSHOT'
        classpath 'org.parchmentmc:librarian:1.+'
    }
}
plugins {
    id 'com.github.johnrengelman.shadow' version '7.1.2'
}
apply plugin: 'net.minecraftforge.gradle'
apply plugin: 'org.spongepowered.mixin'
apply plugin: 'eclipse'
apply plugin: 'maven-publish'
apply plugin: 'org.parchmentmc.librarian.forgegradle'

version = "${mod_version}-mc${mc_version}"
group = 'insane96mcp.survivalreimagined' // http://maven.apache.org/guides/mini/guide-naming-conventions.html
archivesBaseName = 'SurvivalReimagined'

java.toolchain.languageVersion = JavaLanguageVersion.of(17)

println('Java: ' + System.getProperty('java.version') + ' JVM: ' + System.getProperty('java.vm.version') + '(' + System.getProperty('java.vendor') + ') Arch: ' + System.getProperty('os.arch'))
minecraft {
    //mappings channel: 'official', version: "${mc_version}"
    mappings channel: 'parchment', version: '2023.06.26-1.19.4'

    accessTransformer = file('src/main/resources/META-INF/accesstransformer.cfg')

    runs {
        client {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '-mixin.config=survivalreimagined.mixins.json'

            mods {
                survivalreimagined {
                    source sourceSets.main
                }
            }
        }

        server {
            workingDirectory project.file('run_server')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '-mixin.config=survivalreimagined.mixins.json'

            mods {
                survivalreimagined {
                    source sourceSets.main
                }
            }
        }

        data {
            workingDirectory project.file('run')

            property 'forge.logging.markers', 'REGISTRIES'

            property 'forge.logging.console.level', 'debug'

            args '--mod', 'survivalreimagined', '--all', '--output', file('src/generated/resources/'), '--existing', file('src/main/resources/')

            mods {
                survivalreimagined {
                    source sourceSets.main
                }
            }
        }
    }
}

sourceSets.main.resources { srcDir 'src/generated/resources' }

repositories {
    maven { // SlimeKnights and JEI
        name 'DVS1 Maven FS'
        url 'https://dvs1.progwml6.com/files/maven'
    }
    flatDir {
        dir 'libs'
    }
    maven {
        name = "CurseMaven"
        url = "https://www.cursemaven.com/"
        content {
            includeGroup "curse.maven"
        }
    }
    maven {
        url 'https://www.dogforce-games.com/maven/'
        content {
            // Needed otherwise gradle goes crazy by downloading all the dependencies from here (failing)
            includeGroup "dev.gigaherz.sewingkit"
        }
    }
    maven {
        url = "https://jitpack.io"
    }
    // AppleCore
    maven { url "https://maven.ryanliptak.com/" }
}

mixin {
    add sourceSets.main, "survivalreimagined.refmap.json"
}

configurations {
    shade
    implementation.extendsFrom shade
}

dependencies {
    minecraft "net.minecraftforge:forge:${mc_version}-${forge_version}"

    implementation fg.deobf("blank:InsaneLib:${lib_version}-mc${mc_version}")
    runtimeOnly fg.deobf("blank:MobsPropertiesRandomness:${mpr_version}-mc${mc_version}")
    implementation fg.deobf("blank:Shields+:${shields_plus_version}-mc${mc_version}")
    implementation fg.deobf("blank:EnhancedAI:${enhancedai_version}-mc${mc_version}")

    //runtimeOnly fg.deobf("curse.maven:selene-499980:4431044")
    //runtimeOnly fg.deobf("curse.maven:mmmmmmmmmmmm-225738:4410143")
    runtimeOnly fg.deobf("curse.maven:better-advancements-272515:4481186")

    implementation fg.deobf("com.github.glitchfiend:SereneSeasons:${mc_version}-${sereneseasons_version}")

    implementation fg.deobf("com.github.Virtuoel:Pehkui:${pehkui_version}-${mc_version}-forge")
    implementation fg.deobf("curse.maven:kiwi-303657:4501836")
    implementation fg.deobf("curse.maven:passable-foliage-379887:4511198")
    runtimeOnly fg.deobf("curse.maven:tool-belt-260262:4441247")
    //Needed for data gen
    runtimeOnly fg.deobf("dev.gigaherz.sewingkit:SewingKit-${mc_version}:1.8.0")

    runtimeOnly fg.deobf("curse.maven:corail-woodcutter-331983:4442781")

    runtimeOnly fg.deobf("curse.maven:puzzles-lib-495476:4582313")
    runtimeOnly fg.deobf("curse.maven:easy-magic-456239:4556546")

    runtimeOnly fg.deobf("squeek.appleskin:appleskin-forge:mc${mc_version}-2.4.3")

    minecraftLibrary 'com.ezylang:EvalEx:3.0.2'
    annotationProcessor "org.spongepowered:mixin:${mixin_version}:processor"

    shade 'com.ezylang:EvalEx:3.0.2'
}

jar {
    archiveClassifier = 'slim'
    manifest {
        attributes([
                "Specification-Title"     : "survivalreimagined",
                "Specification-Vendor"    : "Insane96MCP",
                "Specification-Version"   : "1", // We are version 1 of ourselves
                "Implementation-Title"    : project.name,
                "Implementation-Version"  : project.jar.archiveVersion,
                "Implementation-Vendor"   : "Insane96MCP",
                "Implementation-Timestamp": new Date().format("yyyy-MM-dd'T'HH:mm:ssZ"),
                "MixinConfigs": "survivalreimagined.mixins.json"
        ])
    }
}

shadowJar {
    archiveClassifier = ''
    configurations = [project.configurations.shade]
    relocate 'com.ezylang.EvalEx', "${project.group}.relocated.EvalEx"
    finalizedBy 'reobfShadowJar'
}

assemble.dependsOn shadowJar

reobf {
    shadowJar {}
}

jar.finalizedBy('reobfJar')

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifact jar
        }
    }
    repositories {
        maven {
            url "file://${project.projectDir}/mcmodsrepo"
        }
    }
}